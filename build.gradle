plugins {
  // determine version from git tags, follow semver spec
  id 'org.ajoberstar.reckon' version '0.12.0'
  // differentiate transient from implementation dependencies
  id 'java-library'
  // manage JavaFx modules
  id 'org.openjfx.javafxplugin' version '0.0.8'
  // publishing plugins
  id 'maven-publish'
  id 'com.jfrog.bintray' version '1.8.4'
  id 'com.jfrog.artifactory' version '4.11.0'
}

group = 'io.reactivex.rxjava2'
description = 'RxJava extensions for JavaFX'

ext {
  vcsUrl = "https://github.com/${github_org}/${github_repo}"
  homepage = vcsUrl
}

reckon {
  scopeFromProp()
  stageFromProp('beta', 'rc', 'final')
}

java {
  sourceCompatibility = JavaVersion.VERSION_11
  targetCompatibility = JavaVersion.VERSION_11
  // tasks and publication artifacts for sources and javadoc
  withJavadocJar()
  withSourcesJar()
}

javafx {
  modules = [ 'javafx.controls', 'javafx.graphics' ]
  version = '11'
  configuration = 'compileOnly'
}

repositories {
    jcenter()
}

dependencies {
    api 'io.reactivex.rxjava2:rxjava:2.2.4'
    testImplementation 'junit:junit-dep:4.11'
    testImplementation 'org.mockito:mockito-core:3.2.0'
}

test {
    moduleOptions {
        runOnClasspath = true
    }
}

javadoc {
    source = sourceSets.main.allJava
    classpath = configurations.compileClasspath

    options {
        setMemberLevel JavadocMemberLevel.PUBLIC
        setAuthor true
//        links 'https://docs.oracle.com/javase/8/docs/api/' // causes build to fail on openjdk-11.0.2+9
    }
}

publishing {
    publications {
        rxjavafx(MavenPublication) {
            from components.java
            pom {
                name = pkg_name
                description = description
                url = homepage

                licenses {
                    license {
                        name = POM_LICENCE_NAME
                        url = POM_SCM_URL
                        distribution = POM_LICENCE_DIST
                    }
                }
                developers {
                    developer {
                        id = 'thomasnield'
                        name = 'Thomas Nield'
                        email = 'thomasnield@live.com'
                        organization = 'ReactiveX'
                        organizationUrl = 'http://reactivex.io/'
                    }
                }
                scm {
                  connection = "scm:git:git://github.com/${github_org}/${github_repo}.git"
                  developerConnection = "scm:git:ssh://github.com/${github_org}/${github_repo}.git"
                  url = vcsUrl
                }
                issueManagement {
                    system = 'github'
                    url = "${vcsUrl}/issues"
                }
            }
        }
    }
}

bintray {
  user = System.getenv('bintray_user')
  key = System.getenv('bintray_api_key')
    publications = ['rxjavafx']
    publish = true
    pkg {
      userOrg = bintray_org
        repo = bintray_repo
        name = pkg_name
        desc = project.description
        licenses = ['Apache-2.0']
        labels = ['rxjava', 'reactivex']
        vcsUrl = vcsUrl
        issueTrackerUrl = "${vcsUrl}/issues"
        websiteUrl = homepage
        version {
            name = "$version"
            vcsTag = "$version"
            gpg {
                sign = true
            }
            mavenCentralSync {
                sync = true
                user = rootProject.sonatypeUsername
                password = rootProject.sonatypePassword

                name = pkg_name
                description = description

                close = '1'
            }
        }

    }
}
